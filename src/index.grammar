@precedence {
  interp
  immediate

  imports
  renamed-import @right

  subscript
  
  dot @left
  single-is-special
  unary
  postfix
  transpose
  typeonlyascription @left
  type-parameters
  
  typeascription @left
  where @left

  prefix
  call

  // https://docs.julialang.org/en/v1/manual/mathematical-operations/#Operator-Precedence-and-Associativity
  power @left
  coefficient
  bitshift @left
  times @left
  plus @left
  colon @left
  pipe @left
  pipeback @right
  contains @left
  band @right
  bor @right

  suffix
  
  ternary @right
  pair @right
  forbinding

  something-statement
  return
    
  baretuple
  assign @right
  arrow @right

  macro-argument
  macro

  array
  array-comprehension

  // This makes it so `[a $b]` is parsed correctly,
  // but it makes `[a $ b]` not work, sadly.
  // But I care more about the former 🤷‍♀️
  matrix
  plus_but_sometimes_interpolation @right

  for-statement
  statement @right

  do

  left0 @left
  right0 @right
  regular0
  regular-1
} 

@top SourceFile {
  expressionList
}

// EXPRESSIONS

expression {  
  blocky_things |
  definition |
  MacroExpression |
  FunctionExpression |

  Identifier |
  boolean |
  Character |
  Number |
  String |
  Symbol |
  
  UnaryExpression |
  BinaryExpression |
  TernaryExpression |
  
  SpreadExpression |
  CoefficientExpression |
  CallExpression |
  Where |
  LonelyType |
  Operator ~op |
  BeginIndex |
  EndIndex |

  InterpolationExpression |
  FieldExpression |
  TransposeExpression |
  SubscriptExpression |
  QuoteExpression |
  TripleString |
  CommandString |
  PrefixedString |
  
  ParenthesizedExpression |
  GeneratorExpression |
  // NOTE TupleExpression actually makes lezer generate FASTER?
  TupleExpression |
  // TODO Discontinued vector might/is still used by some macros
  // .... but it also adds a couple of seconds to the build...
  // .... So changed it for TypeArgumentList for now...
  // .... Not fully accurate, but acurate enough?
  TypeArgumentList |
  ArrayExpression |
  ArrayComprehensionExpression |
  MatrixExpression |
  MatrixExpressionWithOnlySemicolons |
  TypedExpression |
  ParameterizedIdentifier
}


@skip { whitespace | Comment | BlockComment }

// TOP LEVEL

expressionList {
  terminator*
  (
    statement
    (
      terminator+
      (!statement statement)
    )*
    terminator*
  )?
}

// # STATEMENTS
// AssignmentStatement and BareTuple are the only
// nodes that can only exist at the top level of blocks:
// Inside anything the BareTuple can't exist because it becomes
// a normal tuple, and assignment also can't have BareTuple inside
// so it become a AssignmentExpression instead.
statement {
  (expression | AssignmentStatement | BareTuple)
}
BareTuple {
  expression (!baretuple "," expression)+ 
}
AssignmentStatement[@name="AssignmentExpression"] {  
  // TODO Need to get `a, = tuple` working. Curiously, this is the only
  // .... place that a BareTuple can have a trailing comma
  // .... (`tuple = 10,` is not allowed?!)
  // .... Problem is that I always get fighting precedences here,
  // .... which will lezer go like "THEN I WILL ALWAYS MAKE IT AN ASSIGNMENT STATEMENT"
  // I guess I'll make it an external tokenizer :D

  // Want to do `Assignee { statement }` but lezer gets mad and
  // very greedy with making things `Assignee` then...
  
  // (expression | BareTuple !baretuple ","?)          - Slowest
  // (statement | BareTuple !baretuple ","?)  - Mostly slower, but correct
  // statement                                - Fastest
  (expression | BareTuple !baretuple ","?)
  !assign
  (assignOperator | "=")
  statement
}

AssignmentExpression {
  // Using `Assignee` here makes stuff like `c -> x = true` parse as
  // `(c -> x) = true` rather than `c -> (x = true)` 😒
  expression
  !assign
  (assignOperator | "=")
  (expression | AssignmentExpression)
}

// BLOCKY THINGS

blocky_things {
  IfStatement |
  // IfStatement_Pro |
  TryCatchFinally |
  ForStatement |
  WhileStatement |
  LetStatement |
  ConstStatement |
  GlobalStatement |
  LocalStatement |
  QuoteStatement |
  BreakStatement |
  ContinueStatement |
  ReturnStatement |
  ImportStatement |
  ExportStatement |
  CompoundExpression
}

// IfStatement but with block matching working...
// Also creates a less readable tree, so
// TODO Look into how closedBy and openedBy is
// .... interpreted by the codemirror extension.
IfStatement_Pro {
  blockkw<"if", "else elseif end"> 
  IfClause { expression }
    expressionList
  (
    // TODO Should have openedBy="if elseif" too?
    blockkw<"elseif", "else elseif end">
    ElseIfClause { expression }
    expressionList
  )*
  (
    // TODO Should have openedBy "if elseif"?
    blockkw<"else", "end">
    ElseClause { expressionList }
  )?
  end
}

IfStatement {
  IfClause { kw<"if"> expression }
    expressionList
  ElseifClause*
  ElseClause?
  end
}
ElseifClause {
  IfClause { kw<"elseif"> expression }
  expressionList
}
ElseClause {
  kw<"else">
  expressionList
}

TryCatchFinally[@name="TryStatement"] {
  blockkw<"try", "catch finally end">
  expressionList
  Catch?
  Finally?
  end
}
Catch[@name="CatchClause"] {
  blockkw<"catch", "finally end">
  (CatchBinding { Identifier } | terminator)
  expressionList
}
Finally[@name="FinallyClause"] {
  blockkw<"finally", "end">
  expressionList
}

// TODO Should the `in`s be closedBy/openedBy too?
ForStatement {
  !for-statement
  blockkw<"for", "end">
  ForBinding
  ("," ForBinding)*
  expressionList
  end
}

WhileStatement {
  blockkw<"while", "end"> WhileBinding { expression }
    expressionList
  end
}

BreakStatement {
  kw<"break">
}

ContinueStatement {
  kw<"continue">
}

ReturnStatement {
  // kw<"return"> (!return statement)? terminator
  kw<"return"> (!return statement)?
}

LetStatement {
  blockkw<"let", "end">
  LetBinding {
    (expression | AssignmentExpression)
    ("," (expression | AssignmentExpression))*
  }?
  terminator
  expressionList
  end
}

ConstStatement {
  kw<"const"> !something-statement AssignmentStatement
}

GlobalStatement {
  kw<"global"> !something-statement statement
}

LocalStatement {
  kw<"local"> !something-statement statement
}

QuoteStatement {
  blockkw<"quote", "end">
  expressionList
  end
}

/////////////////
// IMPORT STUFF
/////////////////

importable {
  Identifier { word } |
  MacroIdentifier |
  Operator
}

ImportStatement {
  (kw<"using"> | kw<"import">) (Imports | SelectedImport)
}
ExportStatement {
  kw<"export"> importable (!right0 "," importable)*
}

Import {
  "."* importable
  Property { "." importable }*
  Rename { kw<"as"> importable }?
}
Imports {
  Import (!imports "," Import)*
}
SelectedImport {
  Import !renamed-import ":" Imports
}

// DEFINITIONS

definition {
  PrimitiveDefinition |
  AbstractDefinition |
  StructDefinition |
  MutableStructDefinition |
  ModuleDefinition |
  BareModuleDefinition |
  MacroDefinition |
  FunctionDefinition
}
Definition { expression }
FunctionDefinition {
  !regular0 blockkw<"function", "end">
    Definition
    expressionList
  end
}
MacroDefinition {
  blockkw<"macro", "end"> Definition
    expressionList
  end
}

PrimitiveDefinition {
   "primitive type" Definition Number end
}

AbstractDefinition {
  "abstract type" Definition end
}

StructDefinition {
  blockkw<"struct", "end"> Definition !regular0
    expressionList
  end
}
MutableStructDefinition {
  "mutable struct" Definition !regular0
    expressionList
  end
}


ModuleDefinition {
  blockkw<"module", "end"> Definition
    expressionList
  end
}

BareModuleDefinition {
  blockkw<"baremodule", "end"> Definition
    expressionList
  end
}

// @dynamicPrecedence to make this prefered over `Identifier TypeArgumentList`
ParameterizedIdentifier[@dynamicPrecedence=1] {
  expression
  !type-parameters
  immediateBrace
  TypeArgumentList
}

type_argument_list { "{" inner_tuple "}" }
TypeArgumentList {
  type_argument_list
}

FieldName { word }

FieldExpression {
  expression
  !immediate immediateDot !dot "." !dot
  (FieldName | QuoteExpression | InterpolationExpression)
}

SubscriptExpression {
  expression !immediate immediateBracket "[" inner_tuple "]"
}

SpreadExpression {
  expression !dot "..."
}

TernaryExpression {
  expression
  !ternary ternary1 !ternary
  expression
  !ternary ternary2 !ternary
  expression
}

TypedExpression {
  // Value { expression !typeascription }
  expression
  !typeascription
  (operator<"::"> | operator<"<:">)
  Type { !typeascription expression }
}

Where {
  expression
  !where
  kw<"where">
  Type { !where expression }
}

CompoundExpression {
  begin
    expressionList
  end
}

CoefficientExpression {
  (Number | TransposeExpression)
  !coefficient
  (nowhitespace | immediateParen)
  // Need to investigate what `!coefficient` here does
  Coefficient { !coefficient expression }
}

// @dynamicPrecedence is necessary to nudge `x()` to `CallExpression`
// instead of `Indentifier TupleExpression`
CallExpression[@dynamicPrecedence=1] {
  expression
  !call
  BroadcastDot { immediateDot "." }?
  (nowhitespace | immediateParen)
  ArgumentList
}

do_clause_argument {
  Identifier
  Type { !do "::" Identifier }?
}
DoClause {
  blockkw<"do", "end"> 
  (
    // Disabled this for now... broke a lot of odd things...
    // Like with `expression` here it parsed `asd::Vector{Bar}`
    // as `(asd::Vector){Bar}` but it should be (asd)::Vector{Bar}`
    // SO ODD!! WHAT DO DOCLAUSEARGUMENTS HAVE TO DO WITH THAT!
    //
    // DoClauseArguments { expression ("," expression)* } |
    DoClauseArguments { do_clause_argument ("," do_clause_argument)* } |
    terminator
  )
  expressionList
  end
}

ListCompherension {
  expression comprehensionClause
}


LonelyType {
  (operator<"::"> | operator<"<:">)
  Type { !typeonlyascription expression }
}

//////////
// Quoting and interpolation
//////////
InterpolationExpression[@name=Interpolation, @dynamicPrecedence=10] {
  interpolationStart expression interpolationEnd |
  // TODO Make this "$" also InterpolationStart?
  "$" !interp nowhitespace symbol_name
}

Symbol {
  // TODO Make this ":" also QuoteStart?
  ":" !interp nowhitespace symbol_name
}
symbol_name {
  // The reason Symbol and QuoteExpression can co-exist is because
  // Symbol matches on `word`, which is just like identifier BUT
  // will match earlier... so Symbol will "snatch" all the
  // wordy symbols right from QuoteNode...
  // TODO Don't think this distinction is useful though
  Identifier { word } |
  Number |
  String |
  Operator |
  // So assign operators are special because you can't use them as
  // normal identifiers e.g. `+` is valid code, `+=` isn't, while `:+=` is!
  AssignOperatorAsSymbols[@name=Operator] { assignOperator }
  // TODO Allow all operators here explicitly so they get tagged as
  // .... Symbol rather than QuoteExpression (if we keep the distinction)
}

QuoteExpression[@name=Quote,@dynamicPrecedence=10] {
  quoteStart inner_paren quoteEnd |
  // Prefer the inner_paren way
  TupleExpression[@dynamicPrecedence=-1] {
    quoteStart inner_tuple quoteEnd
  } |
  quoteStart AssignOperatorInSymbols[@name=Operator] { assignOperator | "=" } quoteEnd
}


////////////
// Tuples, arguments and ParenthesizedExpression
////////////
tuple_section {
  (
    ListCompherension |
    (expression | AssignmentExpression)
    ~tuple-vs-parens
    (
      ("," (expression | AssignmentExpression))*
      ("," ListCompherension?)?
    )?
  )
}
inner_tuple {
  tuple_section?
  // Calling this `Parameters` now for backwards compatibility...
  // but I feel like all inner_tuples in a TupleExpression should
  // be equal and just "inner" or something
  ((";")+ Parameters { tuple_section })*
  (";")*
}

TupleExpression {
  "(" inner_tuple ")"
}

ArgumentList {
  "(" inner_tuple ")" 
  DoClause?
}

// When a tuple has one element `(a)`
// or only semicolon delimited values `(a;b;c)`
// or even only semicolons with single dangling commas `(a,;b,;c,)`
// it is not treated as a tuple, but as an inline block.
// Thanks to `~tuple-vs-parens` lezer will try both and resort to inline block if it can.
// (TODO I don't like this exception!!)
inner_paren {
  (expression | AssignmentExpression)
  ~tuple-vs-parens
  (";"+ (expression | AssignmentExpression) ","?)*
  ";"*
}
ParenthesizedExpression[@dynamicPrecedence=1] {
  "(" inner_paren ")"
}
GeneratorExpression {
  "(" ListCompherension !single-is-special ")"
}

// This shouldn't have tuple semantics,
// but matrix/array semantics!
DiscontinuedVector {
  "{" inner_tuple "}"
}

//////////////
// Macros!
/////////////

MacroExpression {
  (MacroIdentifier | MacroFieldExpression)
  !macro
  (
    !immediate immediateParen args[@dynamicPrecedence=10] { ArgumentList } | // @use_memo([]) do; ... end
    !immediate immediateBrace TypeArgumentList | // @NamedTuple{x::Int}
    !macro MacroArgumentList // Lame, normal macro call pfffft
  )?
}

MacroFieldExpression {
  expression !immediate immediateDot !dot "." !dot MacroIdentifier |
  MacroIdentifier !immediate immediateDot !dot "." !dot MacroFieldName { word }
}

MacroIdentifier {
  "@" MacroName { word | Operator | Operator[group="operator"] { "." } }
}

MacroArgumentList {
  (!macro-argument statement)+
}

FunctionExpression {
  // (ArgumentList2 { expression } | ArgumentList)
  ArgumentList { !arrow expression !arrow }
  !arrow
  "->"
  !arrow
  (expression | AssignmentExpression)
}

contains_operator { kw<"in"> | kw<"isa"> }
// Needs `ternary2` so it can also match ` : `
// (Else just `a : b` freaks out)
colon_operator { ":" | ternary2 }

// So turns out that if I remove all these and use
// `BinaryExpression { expression !plus "$" expression }`
// only, it about halves the time to generate...
// Just the fact there is so many tokens might overwhelm lezer..
// Maybe switch to ExternalTokenizer here too?
BinaryExpression {
  expression !power powerOperator expression |
  expression !bitshift bitshiftOperator expression |
  expression !pair pairOperator expression |
  expression !colon colon_operator expression |
  expression !times timesOperator expression |
  expression !plus plusOperator expression |
  expression !plus_but_sometimes_interpolation "$" expression |
  expression !arrow arrowOperator expression |
  expression !pipeback pipe_ltr expression |
  expression !pipe pipe_rtl expression |
  expression !contains (contains_operator | comparisonOperator) expression |
  expression !bor "||" expression |
  expression !band "&&" expression
}

UnaryExpression {
  OperatorInUnaryExpression[@name=Operator] { unaryOperatorExtra | plusminus } ~op !unary expression
}

Operator[group="operator"] {
  // !regular0 (    
    "$" |
    bitshiftOperator |
    unaryOperatorExtra |
    plusOperator |
    timesOperator |
    powerOperator |
    comparisonOperator |
    arrowOperator |
    contains_operator |
    pipe_ltr |
    pipe_rtl |
    colon_operator
  // )
}

ArrayExpression {
  "[" "]" |
  "[" expression !array ("," expression)* (",")? "]"
}

// I have this defined in tokens as well...
// Not sure which is better, having the semicolons as one token
// or as x semicolon tokens.
// semicolons_without_spaces[@name="Semicolons"] { ";" (nowhitespace ";")* }

MatrixExpression{
  "["
  !regular-1
  MatrixRow
  (semicolons_without_spaces MatrixRow)* 
  semicolons_without_spaces?
  !regular-1
  "]"
}

// This is SO STUPID but because I love being extremely complete...
// Julia allows `[;;;]` but only if there is nothing between the semicolons,
// no expressions, no commas, no spaces, NO NOTHING
MatrixExpressionWithOnlySemicolons {
  "[" semicolons_without_spaces "]"
}

MatrixRow {
  (!matrix expression)+
}

ArrayComprehensionExpression {
  "[" !array-comprehension expression !array-comprehension comprehensionClause "]"
}

comprehensionClause {
  ForClause (ForClause | IfClause)*
}

IfClause {
  kw<"if"> expression
}

ForClause {
    kw<"for"> ForBinding (!forbinding "," ForBinding)*
}

ForBinding {
  expression AssignOperator { !immediate kw<"in"> | operator<"="> | operator<"∈"> } expression
}

// Even though it is technically "just an unary expression",
// I'm keeping this separate because it needs immediateSingleQuote
// and because I don't feel like rewriting tests
TransposeExpression {
  expression !transpose immediateSingleQuote "'"
}

@skip {} {
  String {
    '"' (stringContent | Interpolation)* '"'
  }
  TripleString {
    '"""' (tripleStringContent | Interpolation)* '"""'
  }
  CommandString {
    "`" (commandStringContent | Interpolation)* "`"
  }

  StringWithoutInterpolation[@name="String"] {
    '"' stringContentWithoutInterpolation '"'
  }
  TripleStringWithoutInterpolation[@name="TripleString"] {
    '"""' tripleStringContentWithoutInterpolation '"""'
  }
  CommandStringWithoutInterpolation[@name="CommandString"] {
    "`" commandStringContentWithoutInterpolation "`"
  }
}

Interpolation {
  "$" Identifier |
  interpolationStart expression interpolationEnd
}

// Removed PrefixFieldExpression because it was grabbing things away from normal FieldExpressions
// PrefixFieldExpression[@name=FieldExpressionFake] {
//   (Identifier | PrefixFieldExpression)
//   !right-2 immediateDot !dot "." !dot
//   FieldName
// }
PrefixedString {
  Prefix { expression !prefix }
  (
    immediateDoubleQuote StringWithoutInterpolation |
    immediateDoubleQuote TripleStringWithoutInterpolation |
    immediateBackquote CommandStringWithoutInterpolation
  )
  Suffix { !suffix nowhitespace word }?
}

operator<term> { [@name={term},group="operator"]{ term } } 
kw<term> { @specialize[@name={term},group="keyword"]<Identifier, term> }
blockkw<term, cc> { @specialize[@name={term},closedBy={cc},group="keyword"]<Identifier, term> }
boolean { @specialize[@name=BooleanLiteral]<Identifier, "true" | "false"> }

@context trackIsIndexing from "./begindexing.tokens.js"
@external specialize {Identifier} begin_end_but_cool from "./begindexing.tokens.js" {
  begin[@name="begin", group="keyword", closedBy="end"]
  BeginIndex
  end[@name="end", group="keyword", openedBy="quote begin if else elseif let try catch finally for while do function struct macro module baremodule struct mutable_struct abstract_type primitive_type"]
  EndIndex
}

@external tokens layoutExtra from "./index.tokens.js" {
  immediateParen,
  immediateColon,
  immediateBrace,
  immediateBracket,
  immediateSingleQuote,
  immediateDoubleQuote,
  immediateBackquote,
  immediateDot,
  nowhitespace
}

terminator[@export="terminator"] { newline | ";" }

@external tokens newline from "./index.tokens.js" { newline }

@external tokens BlockComment from "./index.tokens.js" { BlockComment }

@external tokens tripleStringContent from "./index.tokens.js" { tripleStringContent }
@external tokens stringContent from "./index.tokens.js" { stringContent }
@external tokens commandStringContent from "./index.tokens.js" { commandStringContent }
@external tokens tripleStringContentWithoutInterpolation from "./index.tokens.js" { tripleStringContentWithoutInterpolation }
@external tokens stringContentWithoutInterpolation from "./index.tokens.js" { stringContentWithoutInterpolation }
@external tokens commandStringContentWithoutInterpolation from "./index.tokens.js" { commandStringContentWithoutInterpolation }

// @external tokens operators from "./all_these_operators.tokens.js" {
//   comparisonOperator,
//   arrowOperator,
//   timesOperator,
//   plusOperatorExtra,
//   powerOperator
// } 

plusOperator {
  plusOperatorExtra | plusminus
}

@tokens {
  // These are to export them to the begindexing context tracker
  "["[@export="BracketL"] 
  "]"[@export="BracketR"] 

  // Can implement strings in pure-lezer too?
  // Still have this commented out because feels odd that it works...
  // Also fixed the ExternalTokenizer so no need to fix this yet.
  // @precedence {
  //   "$"
  //   "\"\"\""
  //   stringContent
  //   tripleStringContent
  // }
  // stringContent {
  //   (![\\"$] | "\\" _ | "$" @whitespace)+
  // }
  // tripleStringContent {
  //   (![\\"$] | "\\" _ | "$" @whitespace | "\"" ![\\"$] | "\"" "\"" ![\\"$])+
  // }
  // stringContentWithoutInterpolation {
  //   (![\\"] | "\\" _)+
  // }

  whitespace {
    " " | "\t" | "\n"
  }
  Comment {
    "#" ![\n]*
  }
  Number {
    (
      "0" $[xX] $[0-9a-fA-F] $[0-9a-fA-F_]* |
      $[0-9] $[0-9_]* "."? ($[0-9] $[0-9_]*)? |
      "." $[0-9] $[0-9_]*
    )
    ($[eE] ("-" | "+")? $[0-9]+)?
  }
  powerOperator {
    "."? ("^" | "↑" | "↓" | "⇵" | "⟰" | "⟱" | "⤈" | "⤉" | "⤊" | "⤋" | "⤒" | "⤓" | "⥉" | "⥌" | "⥍" | "⥏" | "⥑" | "⥔" | "⥕" | "⥘" | "⥙" | "⥜" | "⥝" | "⥠" | "⥡" | "⥣" | "⥥" | "⥮" | "⥯" | "￪" | "￬")
  }
  timesOperator {
    "."? ("*" | "/" | "÷" | "%" | "&" | "⋅" | "∘" | "×" | "\\" | "∩" | "∧" | "⊗" | "⊘" | "⊙" | "⊚" | "⊛" | "⊠" | "⊡" | "⊓" | "∗" | "∙" | "∤" | "⅋" | "≀" | "⊼" | "⋄" | "⋆" | "⋇" | "⋉" | "⋊" | "⋋" | "⋌" | "⋏" | "⋒" | "⟑" | "⦸" | "⦼" | "⦾" | "⦿" | "⧶" | "⧷" | "⨇" | "⨰" | "⨱" | "⨲" | "⨳" | "⨴" | "⨵" | "⨶" | "⨷" | "⨸" | "⨻" | "⨼" | "⨽" | "⩀" | "⩃" | "⩄" | "⩋" | "⩍" | "⩎" | "⩑" | "⩓" | "⩕" | "⩘" | "⩚" | "⩜" | "⩞" | "⩟" | "⩠" | "⫛" | "⊍" | "▷" | "⨝" | "⟕" | "⟖" | "⟗")
  }
  plusOperatorExtra {
    "."? ("|||" | "⊕" | "⊖" | "⊞" | "⊟" | "|++|" | "∪" | "∨" | "⊔" | "±" | "∓" | "∔" | "∸" | "≏" | "⊎" | "⊻" | "⊽" | "⋎" | "⋓" | "⧺" | "⧻" | "⨈" | "⨢" | "⨣" | "⨤" | "⨥" | "⨦" | "⨧" | "⨨" | "⨩" | "⨪" | "⨫" | "⨬" | "⨭" | "⨮" | "⨹" | "⨺" | "⩁" | "⩂" | "⩅" | "⩊" | "⩌" | "⩏" | "⩐" | "⩒" | "⩔" | "⩖" | "⩗" | "⩛" | "⩝" | "⩡" | "⩢" | "⩣")
  }
  arrowOperator {
    "."?  ("-->" | "←" | "→" | "↔" | "↚" | "↛" | "↞" | "↠" | "↢" | "↣" | "↦" | "↤" | "↮" | "⇎" | "⇍" | "⇏" | "⇐" | "⇒" | "⇔" | "⇴" | "⇶" | "⇷" | "⇸" | "⇹" | "⇺" | "⇻" | "⇼" | "⇽" | "⇾" | "⇿" | "⟵" | "⟶" | "⟷" | "⟹" | "⟺" | "⟻" | "⟼" | "⟽" | "⟾" | "⟿" | "⤀" | "⤁" | "⤂" | "⤃" | "⤄" | "⤅" | "⤆" | "⤇" | "⤌" | "⤍" | "⤎" | "⤏" | "⤐" | "⤑" | "⤔" | "⤕" | "⤖" | "⤗" | "⤘" | "⤝" | "⤞" | "⤟" | "⤠" | "⥄" | "⥅" | "⥆" | "⥇" | "⥈" | "⥊" | "⥋" | "⥎" | "⥐" | "⥒" | "⥓" | "⥖" | "⥗" | "⥚" | "⥛" | "⥞" | "⥟" | "⥢" | "⥤" | "⥦" | "⥧" | "⥨" | "⥩" | "⥪" | "⥫" | "⥬" | "⥭" | "⥰" | "⧴" | "⬱" | "⬰" | "⬲" | "⬳" | "⬴" | "⬵" | "⬶" | "⬷" | "⬸" | "⬹" | "⬺" | "⬻" | "⬼" | "⬽" | "⬾" | "⬿" | "⭀" | "⭁" | "⭂" | "⭃" | "⭄" | "⭇" | "⭈" | "⭉" | "⭊" | "⭋" | "⭌" | "￩" | "￫" | "⇜" | "⇝" | "↜" | "↝" | "↩" | "↪" | "↫" | "↬" | "↼" | "↽" | "⇀" | "⇁" | "⇄" | "⇆" | "⇇" | "⇉" | "⇋" | "⇌" | "⇚" | "⇛" | "⇠" | "⇢")
  }
  bitshiftOperator {
    "."? (">>" | "<<" | ">>>")
  }
  pairOperator {
    "."? "=>"
  }
  comparisonOperator {
    "."?  ("<:" | ">" | "<" | ">=" | "≥" | "<=" | "≤" | "==" | "===" | "≡" | "!=" | "≠" | "!==" | "≢" | "∈" | "∉" | "∋" | "∌" | "⊆" | "⊈" | "⊂" | "⊄" | "⊊" | "∝" | "∊" | "∍" | "∥" | "∦" | "∷" | "∺" | "∻" | "∽" | "∾" | "≁" | "≃" | "≂" | "≄" | "≅" | "≆" | "≇" | "≈" | "≉" | "≊" | "≋" | "≌" | "≍" | "≎" | "≐" | "≑" | "≒" | "≓" | "≖" | "≗" | "≘" | "≙" | "≚" | "≛" | "≜" | "≝" | "≞" | "≟" | "≣" | "≦" | "≧" | "≨" | "≩" | "≪" | "≫" | "≬" | "≭" | "≮" | "≯" | "≰" | "≱" | "≲" | "≳" | "≴" | "≵" | "≶" | "≷" | "≸" | "≹" | "≺" | "≻" | "≼" | "≽" | "≾" | "≿" | "⊀" | "⊁" | "⊃" | "⊅" | "⊇" | "⊉" | "⊋" | "⊏" | "⊐" | "⊑" | "⊒" | "⊜" | "⊩" | "⊬" | "⊮" | "⊰" | "⊱" | "⊲" | "⊳" | "⊴" | "⊵" | "⊶" | "⊷" | "⋍" | "⋐" | "⋑" | "⋕" | "⋖" | "⋗" | "⋘" | "⋙" | "⋚" | "⋛" | "⋜" | "⋝" | "⋞" | "⋟" | "⋠" | "⋡" | "⋢" | "⋣" | "⋤" | "⋥" | "⋦" | "⋧" | "⋨" | "⋩" | "⋪" | "⋫" | "⋬" | "⋭" | "⋲" | "⋳" | "⋴" | "⋵" | "⋶" | "⋷" | "⋸" | "⋹" | "⋺" | "⋻" | "⋼" | "⋽" | "⋾" | "⋿" | "⟈" | "⟉" | "⟒" | "⦷" | "⧀" | "⧁" | "⧡" | "⧣" | "⧤" | "⧥" | "⩦" | "⩧" | "⩪" | "⩫" | "⩬" | "⩭" | "⩮" | "⩯" | "⩰" | "⩱" | "⩲" | "⩳" | "⩵" | "⩶" | "⩷" | "⩸" | "⩹" | "⩺" | "⩻" | "⩼" | "⩽" | "⩾" | "⩿" | "⪀" | "⪁" | "⪂" | "⪃" | "⪄" | "⪅" | "⪆" | "⪇" | "⪈" | "⪉" | "⪊" | "⪋" | "⪌" | "⪍" | "⪎" | "⪏" | "⪐" | "⪑" | "⪒" | "⪓" | "⪔" | "⪕" | "⪖" | "⪗" | "⪘" | "⪙" | "⪚" | "⪛" | "⪜" | "⪝" | "⪞" | "⪟" | "⪠" | "⪡" | "⪢" | "⪣" | "⪤" | "⪥" | "⪦" | "⪧" | "⪨" | "⪩" | "⪪" | "⪫" | "⪬" | "⪭" | "⪮" | "⪯" | "⪰" | "⪱" | "⪲" | "⪳" | "⪴" | "⪵" | "⪶" | "⪷" | "⪸" | "⪹" | "⪺" | "⪻" | "⪼" | "⪽" | "⪾" | "⪿" | "⫀" | "⫁" | "⫂" | "⫃" | "⫄" | "⫅" | "⫆" | "⫇" | "⫈" | "⫉" | "⫊" | "⫋" | "⫌" | "⫍" | "⫎" | "⫏" | "⫐" | "⫑" | "⫒" | "⫓" | "⫔" | "⫕" | "⫖" | "⫗" | "⫘" | "⫙" | "⫷" | "⫸" | "⫹" | "⫺" | "⊢" | "⊣" | "⟂")
  }
  assignOperator {
    ":=" | "."? ("~" | "=" | "+=" | "-=" | "*=" | "/=" | "//=" | "|=|" | "^=" | "÷=" | "%=" | "<<=" | ">>=" | ">>>=" | "||=|" | "&=" | "⊻=" | "≔" | "⩴" | "≕")
  }
  unaryOperatorExtra {
    "."? (">:" | "!" | "~" | "¬" | "√" | "∛" | "∜")
  }
  plusminus {
    "."? ("+" | "-" | "|")
  }
  Character {
    "'" ("\\" !['] | ![']) "'"
  }

  operator_token {
    bitshiftOperator |
    unaryOperatorExtra |
    plusOperator |
    timesOperator |
    powerOperator |
    comparisonOperator |
    arrowOperator |
    contains_operator |
    pipe_ltr |
    pipe_rtl |
    colon_operator
  }

  pipe_ltr[@name="pipe<"] { "<|" | ".<|" }
  pipe_rtl[@name="pipe>"] { "|>" | ".|>" }

  // semicolons_without_spaces[@name="Semicolons"] { ";"+ }
  semicolons_without_spaces { ";"+ }
  
  ternary1 {
    whitespace+ "?" whitespace+
  }
  ternary2 {
    whitespace+ ":" whitespace+
  }

  // TODO Uncomment this so these will actually show up
  // .... in the tree and not be hidden blegh
  "(" ")" "{" "}" "[" "]" ";" "," "..." "." "$"

  "="

  "abstract type"[@name="abstract_type", group="keyword", closedBy="end"]
  "primitive type"[@name="primitive_type", group="keyword", closedBy="end"]
  "mutable struct"[@name="mutable_struct", group="keyword", closedBy="end"]

  quoteStart[@name=":(", closedBy=")"] { ":(" }
  quoteEnd[@name=")", openedBy=":("] { ")" }

  interpolationStart[@name="$(", closedBy=")"] { "$(" }
  interpolationEnd[@name=")", openedBy="$("] { ")" }
  
  @precedence {
    "||"
    plusOperator
  }
  @precedence {
    "∈"
    comparisonOperator
  }
  
  @precedence {
    Number,
    pipe_ltr,
    pipe_rtl,
    // <: and < will fight over who should be chosen,
    // and if possible it should always be <: (not gonna have a range expression with `<` sadly)
    "<:",
    unaryOperatorExtra
    comparisonOperator,
    "=",
    assignOperator,
    plusOperatorExtra,
    ".",
    ternary1,
    ternary2,
    whitespace
  }
}

// So this is a tricky one... to have `word` for fieldnames and symbols
// but `Identifier` for Identifiers *and keywords*, I now have two tokens that are generated the same way...
// The trick is having `word` earlier in this list, so when it has to chose between `word` or `Identifier`,
// it will always take the `word` first.
@external tokens word from "./index.tokens.js" { word }
@external tokens Identifier from "./index.tokens.js" { Identifier }


@external propSource juliaHighlighting from "./highlight.js"
