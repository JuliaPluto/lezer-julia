@precedence {
  immediate
  dot
  prefix
  postfix
  interp @left
  typeonlyascription @left
  typeascription @left
  coeff
  call
  power @left
  bitshift @left
  times @left
  plus @left
  range @left
  pipe @left
  pipeback @right
  contains @left
  arrow @right
  band @right
  bor @right
  ternary @right
  pair @right
  assign @right
  regular1
  right0 @right
  regular0
  regular-1
  right-2 @right
  regular-2
}

@top SourceFile {
  expressionList | ""
}

@skip { whitespace | Comment | BlockComment }

// TOP LEVEL

expressionList {
  (expression ~id | BareTupleExpression | AssignmentExpression | FunctionAssignmentExpression)
  (!regular0 terminator
    (!regular0 expression | BareTupleExpression | AssignmentExpression | FunctionAssignmentExpression))*
  terminator?
}

BareTupleExpression {
  expression (!regular-1 "," expression)+
}

AssignmentExpression {
  ((expression | BareTupleExpression)
  !assign (assignOperator | "=")
  (expression | AssignmentExpression | BareTupleExpression))
}

simpleAssignmentExpression {
  expression ~id !assign (assignOperator | "=") !assign expression
}

simpleExpressionList {
  (expression ~id | AssignmentExpression { simpleAssignmentExpression })
  (!regular0 terminator
    (!regular0 expression | AssignmentExpression { simpleAssignmentExpression }))*
  terminator?
}

FunctionAssignmentExpression[@dynamicPrecedence=2] {
  Identifier ~id immediateParen !call ArgumentList ~id
  (kw<"where"> TypeArgumentList)?
  !assign (assignOperator | "=")
  !assign (expression | AssignmentExpression)
}

// STATEMENTS

statement {
  IfStatement |
  TryStatement |
  ForStatement |
  WhileStatement |
  LetStatement |
  ConstStatement |
  GlobalStatement |
  LocalStatement |
  QuoteStatement |
  BreakStatement |
  ContinueStatement |
  ReturnStatement |
  ImportStatement |
  ExportStatement
}

IfStatement {
  kw<"if"> expression terminator?
    expressionList?
  ElseifClause*
  ElseClause?
  end
}

ElseifClause {
  kw<"elseif"> expression terminator?
    expressionList?
}

ElseClause {
  kw<"else">
    expressionList?
}

TryStatement {
  kw<"try">
    expressionList?
  CatchClause?
  FinallyClause?
  end
}

CatchClause /* precedence: 1 */ {
  kw<"catch"> Identifier? !regular1 terminator?
    expressionList?
}

FinallyClause {
  kw<"finally"> terminator?
    expressionList?
}

ForStatement {
  !regular0 kw<"for"> ForBinding ("," ForBinding)* terminator?
    expressionList?
  end
}

WhileStatement {
  !regular0 kw<"while"> !regular0 expression !regular0 terminator? !regular0 expressionList? end
}

BreakStatement {
  kw<"break">
}

ContinueStatement {
  kw<"continue">
}

ReturnStatement {
  kw<"return"> (!right-2 (expression | BareTupleExpression))?
}

LetStatement {
  kw<"let"> VariableDeclaration ("," VariableDeclaration)* terminator? expressionList? end
}

ConstStatement {
  kw<"const"> (VariableDeclaration (!right0 "," VariableDeclaration)*)
}

GlobalStatement {
  kw<"global"> (VariableDeclaration (!right0 "," VariableDeclaration)*)
}

LocalStatement {
  kw<"local"> (VariableDeclaration (!right0 "," VariableDeclaration)*)
}

VariableDeclaration /* precedence: right 0 */ {
  (Identifier | InterpolationExpression) (!right0 "=" expression)?
}

QuoteStatement {
  kw<"quote"> expressionList? end
}

ImportStatement {
  !right0 (kw<"using"> | kw<"import">)
  !right0 (Import | SelectedImport)
}

Import {
  (Identifier | ScopedIdentifier) (!right0 "," (Identifier | ScopedIdentifier))*
}

SelectedImport[@dynamicPrecedence=1] {
  (Identifier | ScopedIdentifier)
  !immediate immediateColon ":"
  list {
    (Identifier | MacroIdentifier | Operator)
    (!right0 "," !right0 (Identifier | MacroIdentifier | Operator))*
  }
}

ExportStatement {
  kw<"export"> Identifier (!right0 "," Identifier)*
}

// DEFINITIONS

definition {
  PrimitiveDefinition |
  AbstractDefinition |
  StructDefinition |
  ModuleDefinition |
  BareModuleDefinition |
  MacroDefinition |
  FunctionDefinition
}

FunctionDefinition {
  !regular0 kw<"function">
    (
      Identifier |
      Identifier
      (!immediate immediateBrace !immediate TypeArgumentList)?
      params[@dynamicPrecedence=1] {
        !immediate immediateParen !immediate ArgumentList
        ReturnType {
          !typeascription "::" primaryExpression
        }?
      }
    )
    expressionList? 
  end
}

MacroDefinition {
  kw<"macro"> (Identifier | Operator) immediateParen ArgumentList
    expressionList?
  end
}

PrimitiveDefinition {
  kw<"primitive"> kw<"type"> primaryExpression Number end
}

AbstractDefinition {
  kw<"abstract"> kw<"type"> primaryExpression end
}

StructDefinition {
  kw<"mutable">? kw<"struct"> primaryExpression !regular0 expressionList? end
}

ModuleDefinition {
  kw<"module"> Identifier
    expressionList? 
  end
}

BareModuleDefinition {
  kw<"baremodule"> Identifier
    expressionList?
  end
}

TypeArgumentList {
  "{" expression ("," expression)* ","? "}"
}


// EXPRESSIONS

expression {
  statement |
  definition |
  CompoundExpression |
  PairExpression |
  MacroExpression |
  UnaryExpression |
  BinaryExpression |
  TernaryExpression |
  FunctionExpression |
  CoefficientExpression |
  RangeExpression |
  SpreadExpression |
  Operator { ":" ~op } |
  primaryExpression
}

primaryExpression {
  InterpolationExpression |
  FieldExpression |
  SubscriptExpression |
  QuoteExpression |
  CallExpression |
  Identifier ~id |
  Character |
  Number |
  String |
  TripleString |
  CommandString |
  PrefixedString |
  ParenthesizedExpression |
  TupleExpression |
  ArrayExpression |
  ArrayComprehensionExpression |
  MatrixExpression |
  GeneratorExpression |
  TypedExpression |
  ParameterizedIdentifier |
  Operator ~op
}

ParameterizedIdentifier {
  primaryExpression
  !immediate immediateBrace !immediate
  TypeArgumentList
}

FieldExpression {
  primaryExpression
  !immediate immediateDot !dot "." !dot
  (Identifier | QuoteExpression | InterpolationExpression)
}

SubscriptExpression {
  primaryExpression !immediate immediateBracket "["
    (
      GeneratorExpression { expression comprehensionClause } |
      (expression |
       end |
       AssignmentExpression {
         expression !assign (assignOperator | "=") expression
       })
      ("," (expression |
            end |
            AssignmentExpression {
              expression !assign (assignOperator | "=") expression
            }))*
    )?
    ","?
  "]"
}

SpreadExpression {
  expression !dot "..."
}

PairExpression {
  expression !pair "=>" expression
}

TernaryExpression {
  expression
  !ternary ternary1 !ternary
  expression
  !ternary ternary2 !ternary
  expression
}

TypedExpression {
  primaryExpression !typeascription ("::" | "<:") !typeascription primaryExpression |
  ("::" | "<:") !typeonlyascription primaryExpression
}

InterpolationExpression[@dynamicPrecedence=2] {
  "$" !interp primaryExpression
}

QuoteExpression[@dynamicPrecedence=2] {
  ":" !interp primaryExpression
}

CoefficientExpression[@dynamicPrecedence=1] {
  Number !coeff (ParenthesizedExpression | Identifier)
}

CompoundExpression {
  kw<"begin"> expressionList end
}

CallExpression {
  expression
  !call immediateParen
  !call args[@dynamicPrecedence=1] { ArgumentList }
  DoClause?
}

DoClause {
  kw<"do"> expressionList end
}

ArgumentList {
  "("
  (arg ~id ("," arg ~id)*)?
  (";" arg ("," arg)*)?
  ","?
  ")"
}

arg {
  (expression | NamedArgument { NamedField  }) ~id
}

ParenthesizedExpression {
  "(" !regular1 simpleExpressionList !regular1 ")"
}

TupleExpression {
  "("
    (
    (tupleelem ~id !regular0 ",") |
    (tupleelem ~id (!regular0 "," tupleelem ~id)+ ","?)
    )?
  ")"
}

tupleelem {
  expression | NamedField
}

NamedField {
  name { Identifier | TypedExpression } ~id !assign "=" expression
}

MacroIdentifier {
  !regular0 "@" (Identifier | Operator | Operator { "." })
}

MacroFieldExpression {
  primaryExpression
  !immediate immediateDot !dot "." !dot
  MacroIdentifier
}

FunctionExpression {
  kw<"function"> ArgumentList expressionList? end |
  (Identifier ~id | ArgumentList) !arrow "->" (expression | AssignmentExpression)
}

MacroExpression {
  (MacroIdentifier | MacroFieldExpression) (!right0
    immediateParen args[@dynamicPrecedence=1] { ArgumentList }
    | MacroArgumentList
  )?
}

MacroArgumentList {
  (!regular-2 (expression | AssignmentExpression))+
}

BinaryExpression {
  expression !power powerOperator !power expression |
  expression !bitshift bitshiftOperator !bitshift expression |
  expression !times timesOperator !times expression |
  expression !plus ("$" | plusOperator) !plus expression |
  expression !arrow arrowOperator expression |
  expression !pipeback "<|" expression |
  expression !pipe "|>" expression |
  expression !contains (kw<"in"> | kw<"isa"> | comparisonOperator) !contains expression |
  expression !bor "||" expression |
  expression !band "&&" expression
}

UnaryExpression {
  (unaryOperatorExtra | plusminus) ~op !prefix expression |
  expression !postfix ("'" | ".'")
}

Operator {
  !regular0 (
    "$" |
    plusOperator |
    timesOperator |
    powerOperator |
    comparisonOperator
  )
}

ArrayExpression {
  !regular0 "[" "]" |
  !regular0 "["
    !regular0 expression
    ("," expression)*?
    (!regular0 ",")?
    !regular0 "]"
}

MatrixExpression /* precedence: -1 */ {
  "["
    !regular-1 MatrixRow
    (";" MatrixRow)* 
    (!regular-1 ";")?
    !regular-1 "]"
}

MatrixRow {
  /* precedence: -1 */ (!regular-1 expression)+
}

ArrayComprehensionExpression {
  "[" !regular0 expression !regular0 comprehensionClause "]"
}

GeneratorExpression {
  "(" expression comprehensionClause ")"
}

comprehensionClause {
  ForClause (ForClause | IfClause)*
}

IfClause {
  kw<"if"> expression
}

ForClause {
  kw<"for"> ForBinding (!regular0 "," ForBinding)*
}

ForBinding {
  (Identifier | TupleExpression) (kw<"in"> | "=" | "∈") expression
}

ScopedIdentifier {
  (Identifier | ScopedIdentifier)? !dot "." Identifier
}

RangeExpression {
  expression !range ":" !range expression
}

@skip {} {
  String {
    '"' (stringContent | stringInterpolation)* '"'
  }
  TripleString {
    '"""' (tripleStringContent | stringInterpolation)* '"""'
  }
  CommandString {
    '`' (commandStringContent | stringInterpolation)* '`'
  }
}

stringInterpolation {
  "$" Identifier |
  "$" immediateParen "(" !regular1 expression !regular1 ")"
}

PrefixedString {
  Identifier !immediate immediateDoubleQuote String |
  Identifier !immediate immediateDoubleQuote TripleString |
  Identifier !immediate immediateBackquote CommandString
}

plusOperator {
  "." !immediate nowhitespace (plusOperatorExtra | plusminus) |
  (plusOperatorExtra | plusminus)
}

end { @specialize[@name=end]<Identifier, "end"> }

kw<term> { @specialize[@name={term}]<Identifier, term> }

@external tokens terminator from "./index.tokens.js" { terminator }
@external tokens Identifier from "./index.tokens.js" { Identifier }
@external tokens BlockComment from "./index.tokens.js" { BlockComment }
@external tokens tripleStringContent from "./index.tokens.js" { tripleStringContent }
@external tokens stringContent from "./index.tokens.js" { stringContent }
@external tokens commandStringContent from "./index.tokens.js" { commandStringContent }
@external tokens layoutExtra from "./index.tokens.js" {
  immediateParen,
  immediateColon,
  immediateBrace,
  immediateBracket,
  immediateDoubleQuote,
  immediateBackquote,
  immediateDot,
  nowhitespace
}

@tokens {
  whitespace {
    " " | "\t" | "\n"
  }
  Comment {
    "#" ![\n]*
  }
  Number {
    (
      "0" $[xX] $[0-9a-fA-F] $[0-9a-fA-F_]* |
      $[0-9] $[0-9_]* "."? ($[0-9] $[0-9_]*)? |
      "." $[0-9] $[0-9_]*
    )
    ($[eE] ("-" | "+")? $[0-9]+)?
  }
  powerOperator {
    "."? ("^" | "↑" | "↓" | "⇵" | "⟰" | "⟱" | "⤈" | "⤉" | "⤊" | "⤋" | "⤒" | "⤓" | "⥉" | "⥌" | "⥍" | "⥏" | "⥑" | "⥔" | "⥕" | "⥘" | "⥙" | "⥜" | "⥝" | "⥠" | "⥡" | "⥣" | "⥥" | "⥮" | "⥯" | "￪" | "￬")
  }
  timesOperator {
    "."? ("*" | "/" | "÷" | "%" | "&" | "⋅" | "∘" | "×" | "\\" | "∩" | "∧" | "⊗" | "⊘" | "⊙" | "⊚" | "⊛" | "⊠" | "⊡" | "⊓" | "∗" | "∙" | "∤" | "⅋" | "≀" | "⊼" | "⋄" | "⋆" | "⋇" | "⋉" | "⋊" | "⋋" | "⋌" | "⋏" | "⋒" | "⟑" | "⦸" | "⦼" | "⦾" | "⦿" | "⧶" | "⧷" | "⨇" | "⨰" | "⨱" | "⨲" | "⨳" | "⨴" | "⨵" | "⨶" | "⨷" | "⨸" | "⨻" | "⨼" | "⨽" | "⩀" | "⩃" | "⩄" | "⩋" | "⩍" | "⩎" | "⩑" | "⩓" | "⩕" | "⩘" | "⩚" | "⩜" | "⩞" | "⩟" | "⩠" | "⫛" | "⊍" | "▷" | "⨝" | "⟕" | "⟖" | "⟗")
  }
  plusOperatorExtra {
    "|||" | "⊕" | "⊖" | "⊞" | "⊟" | "|++|" | "∪" | "∨" | "⊔" | "±" | "∓" | "∔" | "∸" | "≂" | "≏" | "⊎" | "⊻" | "⊽" | "⋎" | "⋓" | "⧺" | "⧻" | "⨈" | "⨢" | "⨣" | "⨤" | "⨥" | "⨦" | "⨧" | "⨨" | "⨩" | "⨪" | "⨫" | "⨬" | "⨭" | "⨮" | "⨹" | "⨺" | "⩁" | "⩂" | "⩅" | "⩊" | "⩌" | "⩏" | "⩐" | "⩒" | "⩔" | "⩖" | "⩗" | "⩛" | "⩝" | "⩡" | "⩢" | "⩣"
  }
  arrowOperator {
    "--" | "-->" | "."?  ("←" | "→" | "↔" | "↚" | "↛" | "↞" | "↠" | "↢" | "↣" | "↦" | "↤" | "↮" | "⇎" | "⇍" | "⇏" | "⇐" | "⇒" | "⇔" | "⇴" | "⇶" | "⇷" | "⇸" | "⇹" | "⇺" | "⇻" | "⇼" | "⇽" | "⇾" | "⇿" | "⟵" | "⟶" | "⟷" | "⟹" | "⟺" | "⟻" | "⟼" | "⟽" | "⟾" | "⟿" | "⤀" | "⤁" | "⤂" | "⤃" | "⤄" | "⤅" | "⤆" | "⤇" | "⤌" | "⤍" | "⤎" | "⤏" | "⤐" | "⤑" | "⤔" | "⤕" | "⤖" | "⤗" | "⤘" | "⤝" | "⤞" | "⤟" | "⤠" | "⥄" | "⥅" | "⥆" | "⥇" | "⥈" | "⥊" | "⥋" | "⥎" | "⥐" | "⥒" | "⥓" | "⥖" | "⥗" | "⥚" | "⥛" | "⥞" | "⥟" | "⥢" | "⥤" | "⥦" | "⥧" | "⥨" | "⥩" | "⥪" | "⥫" | "⥬" | "⥭" | "⥰" | "⧴" | "⬱" | "⬰" | "⬲" | "⬳" | "⬴" | "⬵" | "⬶" | "⬷" | "⬸" | "⬹" | "⬺" | "⬻" | "⬼" | "⬽" | "⬾" | "⬿" | "⭀" | "⭁" | "⭂" | "⭃" | "⭄" | "⭇" | "⭈" | "⭉" | "⭊" | "⭋" | "⭌" | "￩" | "￫" | "⇜" | "⇝" | "↜" | "↝" | "↩" | "↪" | "↫" | "↬" | "↼" | "↽" | "⇀" | "⇁" | "⇄" | "⇆" | "⇇" | "⇉" | "⇋" | "⇌" | "⇚" | "⇛" | "⇠" | "⇢")
  }
  bitshiftOperator {
    ">>" | "<<" | ">>>"
  }
  comparisonOperator {
    "|<:|" | "|>:|" | "."?  (">" | "<" | ">=" | "≥" | "<=" | "≤" | "==" | "===" | "≡" | "!=" | "≠" | "!==" | "≢" | "∈" | "∉" | "∋" | "∌" | "⊆" | "⊈" | "⊂" | "⊄" | "⊊" | "∝" | "∊" | "∍" | "∥" | "∦" | "∷" | "∺" | "∻" | "∽" | "∾" | "≁" | "≃" | "≂" | "≄" | "≅" | "≆" | "≇" | "≈" | "≉" | "≊" | "≋" | "≌" | "≍" | "≎" | "≐" | "≑" | "≒" | "≓" | "≖" | "≗" | "≘" | "≙" | "≚" | "≛" | "≜" | "≝" | "≞" | "≟" | "≣" | "≦" | "≧" | "≨" | "≩" | "≪" | "≫" | "≬" | "≭" | "≮" | "≯" | "≰" | "≱" | "≲" | "≳" | "≴" | "≵" | "≶" | "≷" | "≸" | "≹" | "≺" | "≻" | "≼" | "≽" | "≾" | "≿" | "⊀" | "⊁" | "⊃" | "⊅" | "⊇" | "⊉" | "⊋" | "⊏" | "⊐" | "⊑" | "⊒" | "⊜" | "⊩" | "⊬" | "⊮" | "⊰" | "⊱" | "⊲" | "⊳" | "⊴" | "⊵" | "⊶" | "⊷" | "⋍" | "⋐" | "⋑" | "⋕" | "⋖" | "⋗" | "⋘" | "⋙" | "⋚" | "⋛" | "⋜" | "⋝" | "⋞" | "⋟" | "⋠" | "⋡" | "⋢" | "⋣" | "⋤" | "⋥" | "⋦" | "⋧" | "⋨" | "⋩" | "⋪" | "⋫" | "⋬" | "⋭" | "⋲" | "⋳" | "⋴" | "⋵" | "⋶" | "⋷" | "⋸" | "⋹" | "⋺" | "⋻" | "⋼" | "⋽" | "⋾" | "⋿" | "⟈" | "⟉" | "⟒" | "⦷" | "⧀" | "⧁" | "⧡" | "⧣" | "⧤" | "⧥" | "⩦" | "⩧" | "⩪" | "⩫" | "⩬" | "⩭" | "⩮" | "⩯" | "⩰" | "⩱" | "⩲" | "⩳" | "⩵" | "⩶" | "⩷" | "⩸" | "⩹" | "⩺" | "⩻" | "⩼" | "⩽" | "⩾" | "⩿" | "⪀" | "⪁" | "⪂" | "⪃" | "⪄" | "⪅" | "⪆" | "⪇" | "⪈" | "⪉" | "⪊" | "⪋" | "⪌" | "⪍" | "⪎" | "⪏" | "⪐" | "⪑" | "⪒" | "⪓" | "⪔" | "⪕" | "⪖" | "⪗" | "⪘" | "⪙" | "⪚" | "⪛" | "⪜" | "⪝" | "⪞" | "⪟" | "⪠" | "⪡" | "⪢" | "⪣" | "⪤" | "⪥" | "⪦" | "⪧" | "⪨" | "⪩" | "⪪" | "⪫" | "⪬" | "⪭" | "⪮" | "⪯" | "⪰" | "⪱" | "⪲" | "⪳" | "⪴" | "⪵" | "⪶" | "⪷" | "⪸" | "⪹" | "⪺" | "⪻" | "⪼" | "⪽" | "⪾" | "⪿" | "⫀" | "⫁" | "⫂" | "⫃" | "⫄" | "⫅" | "⫆" | "⫇" | "⫈" | "⫉" | "⫊" | "⫋" | "⫌" | "⫍" | "⫎" | "⫏" | "⫐" | "⫑" | "⫒" | "⫓" | "⫔" | "⫕" | "⫖" | "⫗" | "⫘" | "⫙" | "⫷" | "⫸" | "⫹" | "⫺" | "⊢" | "⊣" | "⟂")
  }
  assignOperator {
    ":=" | "~" | "$=" | "."? ("=" | "+=" | "-=" | "*=" | "/=" | "//=" | "|=|" | "^=" | "÷=" | "%=" | "<<=" | ">>=" | ">>>=" | "||=|" | "&=" | "⊻=" | "≔" | "⩴" | "≕")
  }
  unaryOperatorExtra {
    ">:" | "!" | "~" | "¬" | "√" | "∛" | "∜"
  }
  plusminus {
    "+" | "-"
  }
  Character {
    "'" ("\\" !['] | ![']) "'"
  }
  ternary1 {
    whitespace+ "?" whitespace+
  }
  ternary2 {
    whitespace+ ":" whitespace+
  }
  @precedence {
    Number,
    comparisonOperator,
    "=",
    assignOperator,
    unaryOperatorExtra,
    plusOperatorExtra,
    ".",
    ternary1,
    ternary2,
    whitespace
  }
}
