@precedence {
  immediate
  dot @left
  prefix
  postfix
  interp @left
  typeonlyascription @left
  typeascription @left
  coeff
  call
  power @left
  bitshift @left
  times @left
  plus @left
  transpose @left
  range @left
  pipe @left
  pipeback @right
  contains @left
  arrow @right
  band @right
  bor @right
  ternary @right
  pair @right
  forbinding
  baretuple
  assign @right
  assignstatement
  regular1
  right0 @right
  regular0
  regular-1
  right-2 @right
  regular-2
}

@top SourceFile {
  expressionList<e, pe> | ""
}

@skip { whitespace | Comment | BlockComment }

// TOP LEVEL

expressionList<e, pe> {
  !right0
  terminator* ~possibly_more_expressions
  !right0 
  (e ~id | BareTupleExpression<e> | AssignmentExpression<e> | FunctionAssignmentExpression)?
  (!right0 terminator+ ~possibly_more_expressions
    (!right0 e ~id | BareTupleExpression<e> | AssignmentExpression<e> | FunctionAssignmentExpression))*
  terminator* ~possibly_more_expressions
}

BareTupleExpression<e> {
  e (!baretuple "," e)+
}

AssignmentExpression<e> {
  ((e | BareTupleExpression<e>)
  !assign (assignOperator | "=")
  (e | AssignmentExpression<e> | BareTupleExpression<e>))
}

simpleAssignmentExpression<e> {
  e ~id !assign (assignOperator | "=") !assign e
}

simpleExpressionList<e> {
  (e ~id | AssignmentExpression { simpleAssignmentExpression<e> })
  (!regular0 terminator
    (!regular0 e | AssignmentExpression { simpleAssignmentExpression<e> }))*
  terminator?
}

FunctionAssignmentExpression[@dynamicPrecedence=2] {
  Identifier ~id immediateParen !call ArgumentList<e, pe> ~id
  (kw<"where"> TypeArgumentList<e>)?
  !assign (assignOperator | "=")
  !assign (e | AssignmentExpression<e>)
}

// STATEMENTS

statement {
  IfStatement |
  TryStatement |
  ForStatement |
  WhileStatement |
  LetStatement |
  ConstStatement |
  GlobalStatement |
  LocalStatement |
  QuoteStatement |
  BreakStatement |
  ContinueStatement |
  ReturnStatement |
  ImportStatement |
  ExportStatement
}

IfStatement[closedBy="end else elseif"] {
  IfClause { kw<"if"> e }
    expressionList<e, pe>
  ElseifClause*
  ElseClause?
  end
}

ElseifClause[closedBy="end else elseif"] {
  IfClause { kw<"elseif"> e }
    expressionList<e, pe>
}

ElseClause[closedBy="end"] {
  kw<"else">
    expressionList<e, pe>
}

TryStatement[closedBy="end catch finally"] {
  kw<"try">
    expressionList<e, pe>
  CatchClause?
  FinallyClause?
  end
}

CatchClause[closedBy="end finally"] {
  kw<"catch"> CatchBinding { Identifier ~id }? !regular1
    expressionList<e, pe>
}

FinallyClause[closedBy="end"] {
  kw<"finally">
    expressionList<e, pe>
}

ForStatement[closedBy="end"] {
  !regular0 kw<"for"> ForBinding<e, pe> ("," ForBinding<e, pe>)*
    expressionList<e, pe>
  end
}

WhileStatement[closedBy="end"] {
  !regular0 kw<"while"> !regular0 WhileBinding { e } !regular0
    expressionList<e, pe>
  end
}

BreakStatement {
  kw<"break">
}

ContinueStatement {
  kw<"continue">
}

ReturnStatement {
  kw<"return"> (!right-2 (e | BareTupleExpression<e>))?
}

LetStatement[closedBy="end"] {
  kw<"let"> LetBinding { VariableDeclaration ("," VariableDeclaration)* }? terminator
    expressionList<e, pe>
  end
}

ConstStatement {
  kw<"const"> AssignmentExpression<e>
}

GlobalStatement {
  kw<"global"> (
    global_assignment[@dynamicPrecedence=10] { AssignmentExpression<e> } |
    Identifier ~variabledeclaration |
    InterpolationExpression<pe> ~variabledeclaration
  )
}

LocalStatement {
  kw<"local"> (
    global_assignment[@dynamicPrecedence=10] { AssignmentExpression<e> } |
    Identifier ~variabledeclaration |
    InterpolationExpression<pe> ~variabledeclaration
  )
}

VariableDeclaration {
  (Identifier | InterpolationExpression<pe>) (!right0 "=" e)?
}

QuoteStatement[closedBy="end"] {
  kw<"quote"> !right-2
    expressionList<e, pe>
  end
}

ImportStatement {
  !right0 (kw<"using"> | kw<"import">)
  !right0 (Import | SelectedImport)
}

Import {
  (Identifier | ScopedIdentifier | RenamedIdentifier)
  (!right0 "," (Identifier | ScopedIdentifier | RenamedIdentifier))*
}

SelectedImport[@dynamicPrecedence=1] {
  (Identifier | ScopedIdentifier)
  !immediate immediateColon ":"
  list {
    (RenamedImport | Identifier | MacroIdentifier | Operator)
    (!right0 "," !right0 (RenamedImport | Identifier | MacroIdentifier | Operator))*
  }
}

RenamedIdentifier {
  (Identifier | ScopedIdentifier) kw<"as"> Identifier
}

RenamedImport {
  (Identifier | ScopedIdentifier | MacroIdentifier | Operator) kw<"as"> (Identifier | MacroIdentifier | Operator)
}

ExportStatement {
  kw<"export"> (Identifier | MacroIdentifier) (!right0 "," (Identifier | MacroIdentifier))*
}

// DEFINITIONS

definition {
  PrimitiveDefinition |
  AbstractDefinition |
  StructDefinition |
  ModuleDefinition |
  BareModuleDefinition |
  MacroDefinition |
  FunctionDefinition
}

FunctionDefinition[closedBy="end"] {
  !regular0 kw<"function">
    Definition { definitionHead }
    params[@dynamicPrecedence=1] {
      !immediate immediateParen !immediate ArgumentList<e, pe> !typeascription
      ReturnType {
        !typeascription operator<"::"> TypePart
      }?
      (kw<"where"> TypeParameters { Identifier | TypeArgumentList<e> } )?
    }?
    expressionList<e, pe>
  end
}

MacroDefinition[closedBy="end"] {
  kw<"macro"> Definition { definitionHead } immediateParen ArgumentList<e, pe>
    expressionList<e, pe>
  end
}

PrimitiveDefinition[closedBy="end"] {
  kw<"primitive"> kw<"type"> Definition { pe } Number end
}

AbstractDefinition[closedBy="end"] {
  kw<"abstract"> kw<"type"> Definition { pe } end
}

StructDefinition[closedBy="end"] {
  kw<"mutable">? kw<"struct"> Definition { pe } !regular0
    expressionList<e, pe>
  end
}

ModuleDefinition[closedBy="end"] {
  kw<"module"> Definition { Identifier }
    expressionList<e, pe>
  end
}

BareModuleDefinition[closedBy="end"] {
  kw<"baremodule"> Definition { Identifier }
    expressionList<e, pe>
  end
}

definitionHead {
  InterpolationExpression<definitionHead> |
  FieldExpression<definitionHead> |
  SubscriptExpression<definitionHead> |
  QuoteExpression<definitionHead> |
  ParameterizedIdentifier<e, definitionHead> |
  Identifier ~id |
  ParenthesizedExpression<definitionHead> |
  Operator ~op
}

// EXPRESSIONS

e {
  statement |
  definition |
  CompoundExpression |
  PairExpression<e> |
  MacroExpression<e, pe> |
  UnaryExpression<e> |
  BinaryExpression<e> |
  TernaryExpression<e> |
  FunctionExpression<e, pe> |
  FunctionExpression[closedBy="end"] { verboseFunctionExpression } |
  CoefficientExpression<e> |
  RangeExpression<e> |
  TransposeExpression<e> |
  SpreadExpression<e> |
  Operator[group="operator"] { ":" ~op } |
  pe
}

meta_programming<e, pe> {
  PrefixedString |
  MacroExpression<e, pe> |
  InterpolationExpression<pe>
}

pe {
  InterpolationExpression<pe> ~variabledeclaration |
  FieldExpression<pe> |
  SubscriptExpression<pe> |
  Symbol |
  QuoteExpression<pe> |
  CallExpression<e, pe> |
  Identifier ~id ~variabledeclaration |
  boolean |
  Character |
  Number ~afternumber |
  String |
  TripleString |
  CommandString |
  PrefixedString |
  ParenthesizedExpression<e> |
  TupleExpression<e, pe> |
  ArrayExpression<e> |
  ArrayComprehensionExpression<e, pe> |
  MatrixExpression<e> |
  GeneratorExpression<e, pe> |
  TypedExpression<pe> |
  ParameterizedIdentifier<e, pe> |
  Operator ~op
}

ParameterizedIdentifier<e, pe> {
  pe
  !immediate immediateBrace !immediate
  TypeArgumentList<e>
}

TypeArgumentList[closedBy="}"]<e> {
  "{" e ("," e)* ","? "}"
}

FieldName { word }

FieldExpression<pe> {
  pe
  !immediate immediateDot !dot "." !dot
  (FieldName | QuoteExpression<pe> | InterpolationExpression<pe>)
}

SubscriptExpression[closedBy="]"]<pe> {
  pe !immediate immediateBracket "["
    (
      ListCompherension<s_e, s_pe> |
      // GeneratorExpression { s_e comprehensionClause<s_e, s_pe> } |
      (s_e |
       AssignmentExpression {
         s_e !assign (assignOperator | "=") s_e
       })
      ("," (s_e |
            AssignmentExpression {
              s_e !assign (assignOperator | "=") s_e
            }))*
    )?
    ","?
  "]"
}

s_e {
  statement |
  definition |
  // CompoundExpression |
  PairExpression<s_e> |
  MacroExpression<s_e, s_pe> |
  UnaryExpression<s_e> |
  BinaryExpression<s_e> |
  TernaryExpression<s_e> |
  FunctionExpression<s_e, s_pe> |
  CoefficientExpression<s_e> |
  RangeExpression<s_e> |
  TransposeExpression<s_e> |
  SpreadExpression<s_e> |
  Operator[group="operator"] { ":" ~op } |
  s_pe
}

s_pe {
  InterpolationExpression<s_pe> |
  FieldExpression<s_pe> |
  SubscriptExpression<s_pe> |
  Symbol |
  QuoteExpression<s_pe> |
  CallExpression<s_e, s_pe> |
  Identifier ~id |
  boolean |
  Character |
  Number ~afternumber |
  String |
  TripleString |
  CommandString |
  PrefixedString |
  ParenthesizedExpression<s_e> |
  TupleExpression<s_e, s_pe> |
  ArrayExpression<s_e> |
  ArrayComprehensionExpression<s_e, s_pe> |
  MatrixExpression<s_e> |
  GeneratorExpression<s_e, s_pe> |
  TypedExpression<s_pe> |
  ParameterizedIdentifier<s_e, s_pe> |
  Operator ~op |
  kw<"begin"> |
  kw<"end">
}

SpreadExpression<pe> {
  pe !dot "..."
}

PairExpression<e> {
  e !pair "=>" e
}

TernaryExpression<e> {
  e
  !ternary ternary1 !ternary
  e
  !ternary ternary2 !ternary
  e
}

TypePart[@name="Type"] { pe }
TypedExpression<pe> {
  pe !typeascription (operator<"::"> | operator<"<:">) !typeascription TypePart
}

InterpolationExpression[@dynamicPrecedence=2]<pe> {
  "$" !interp pe
}

Symbol[@dynamicPrecedence=10] {
  ":" !interp SymbolName { word }
}

QuoteExpression[@dynamicPrecedence=2]<pe> {
  ":" !interp pe
}

// I guess there might be more thing "coeffecientable",
// but for now this is the list
Coefficient {
  CallExpression {
    Identifier
    !call immediateParen
    !call args[@dynamicPrecedence=1] { ArgumentList<e, pe> }
  } |
  Identifier |
  FieldExpression<Identifier> |
  InterpolationExpression<pe> |
  PrefixedString |
  ParenthesizedExpression<e> |
  TupleExpression<e, pe>
}
CoefficientExpression[@dynamicPrecedence=5]<e> {
  Number ~afternumber !coeff (nowhitespace | immediateParen) ~nowhitespace Coefficient
}

CompoundExpression[closedBy="end"] {
  kw<"begin">
    expressionList<e, pe>
  end
}

CallExpression<e, pe> {
  pe
  !call immediateParen ~nowhitespace
  !call args[@dynamicPrecedence=1] { ArgumentList<e, pe> }
  DoClause?
}

DoClause[closedBy="end"] {
  kw<"do"> 
  DoClauseArguments {
    (Identifier ~id | BareTupleExpression<e> | ArgumentList<e, pe>)
    expressionList<e, pe>
  }?
  ~possibly_more_expressions
  !regular1
  terminator
  expressionList<e, pe>
  end
}

ListCompherension<e, pe> {
  e ~id comprehensionClause<e, pe> ~generator
}

ArgumentList[closedBy=")"]<e, pe> {
  "("
  (arg<e, pe> ~id ("," arg<e, pe> ~id)*)?
  ListCompherension<e, pe>?
  (";" arg<e, pe> ~id ("," arg<e, pe> ~id)*)?
  ","?  
  ")" ~afterclosingparen
}

LonelyType {
  (operator<"::"> | operator<"<:">) !typeonlyascription TypePart
}

arg<e, pe> {
  (e | LonelyType | NamedArgument { NamedField<e, pe>  }) ~id
}

ParenthesizedExpression[closedBy=")"]<e> {
  "(" !regular1 simpleExpressionList<e> !regular1 ")"
}

TupleExpression[closedBy=")"]<e, pe> {
  "("
    (
      (tupleelem<e, pe> ~id !regular0 ",") ListCompherension<e, pe>? |
      (tupleelem<e, pe> ~id (!regular0 "," tupleelem<e, pe> ~id)+ ","?) ListCompherension<e, pe>?
    )?
  ")" ~afterclosingparen
}

tupleelem<e, pe> {
  e | NamedField<e, pe>
}

NamedField<e, pe> {
  name { Identifier | TypedExpression<pe> } ~id !assign "=" e
}

MacroIdentifier {
  !regular0 "@" MacroName { word | Operator | Operator[group="operator"] { "." } }
}

MacroFieldExpression<pe> {
  pe !immediate immediateDot !dot "." !dot MacroIdentifier |
  MacroIdentifier !immediate immediateDot !dot "." !dot MacroFieldName { word }
}

verboseFunctionExpression {
  kw<"function"> ArgumentList<e, pe> expressionList<e, pe> end
}

FunctionExpression<e, pe> {
  (Identifier ~id | ArgumentList<e, pe>) !arrow "->" (e | AssignmentExpression<e>)
}

MacroExpression<e, pe> {
  (MacroIdentifier | MacroFieldExpression<pe>) (!right0
    immediateParen args[@dynamicPrecedence=1] { ArgumentList<e, pe> } DoClause?
    | MacroArgumentList<e>
  )?
}

MacroArgumentList<e> {
  (!regular-2 (e | AssignmentExpression<e>))+
}

BinaryExpression<e> {
  e !power powerOperator !power e |
  e !bitshift bitshiftOperator !bitshift e |
  e !times timesOperator !times e |
  e !plus ("$" | plusOperator) !plus e |
  e !arrow arrowOperator e |
  e !pipeback "<|" e |
  e !pipe "|>" e |
  e !contains (kw<"in"> | kw<"isa"> | comparisonOperator) !contains e |
  e !bor "||" e |
  e !band "&&" e
}

UnaryExpression<e> {
  (unaryOperatorExtra | plusminus) ~op !prefix e |
  e !postfix ("'" | ".'")
}

Operator[group="operator"] {
  !regular0 (
    "$" |
    "=" |
    assignOperator |
    plusOperator |
    timesOperator |
    powerOperator |
    comparisonOperator
  )
}

ArrayExpression[closedBy="]"]<e> {
  !regular0 "[" "]" |
  !regular0 "["
    !regular0 e
    ("," e)*?
    (!regular0 ",")?
    !regular0 "]"
}

MatrixExpression[closedBy="]"]<e> {
  "["
    !regular-1 MatrixRow<e>
    (";" MatrixRow<e>)* 
    (!regular-1 ";")?
    !regular-1 "]"
}

MatrixRow<e> {
  (!regular-1 e)+
}

ArrayComprehensionExpression[closedBy="]"]<e, pe> {
  "[" !regular0 e !regular0 comprehensionClause<e, pe> "]"
}

GeneratorExpression[closedBy=")"]<e, pe> {
  "(" ListCompherension<e, pe> ")" ~afterclosingparen
}

comprehensionClause<e, pe> {
  ForClause<e, pe> (ForClause<e, pe> | IfClause<e, pe>)*
}

IfClause<e, pe> {
  kw<"if"> e
}

ForClause<e, pe> {
    kw<"for"> ForBinding<e, pe> (!forbinding "," ForBinding<e, pe>)*
}

ForBinding<e, pe> {
  (Identifier ~id | TupleExpression<e, pe>) (kw<"in"> | operator<"="> | operator<"∈">) e
}

ScopedIdentifier {
  "." !dot ScopedIdentifier |
  (Identifier | ScopedIdentifier)? !dot "." !dot Identifier
}

RangeExpression<e> {
  e !range ":" !range e
}

TransposeExpression<e> {
  e !transpose "'" !transpose e
}

@skip {} {
  String {
    '"' (stringContent | stringInterpolation)* '"'
  }
  TripleString {
    '"""' (tripleStringContent | stringInterpolation)* '"""'
  }
  CommandString {
    "`" (commandStringContent | stringInterpolation)* "`"
  }

  StringWithoutInterpolation {
    '"' stringContentWithoutInterpolation '"'
  }
  TripleStringWithoutInterpolation {
    '"""' tripleStringContentWithoutInterpolation '"""'
  }
  CommandStringWithoutInterpolation {
    "`" commandStringContentWithoutInterpolation "`"
  }
}

stringInterpolation {
  operator<"$"> Identifier |
  operator<"$"> immediateParen operator<"("> !regular1 e !regular1 operator<")">
}

PrefixFieldExpression[@name=FieldEpxression] {
  PrefixFieldExpression
  !immediate immediateDot !dot "." !dot
  FieldName
}
PrefixedString {
  Prefix { Identifier | PrefixFieldExpression }
  !immediate
  
  (immediateDoubleQuote StringWithoutInterpolation |
  immediateDoubleQuote TripleStringWithoutInterpolation |
  immediateBackquote CommandStringWithoutInterpolation)
}

plusOperator {
  "." !immediate nowhitespace (plusOperatorExtra | plusminus) |
  (plusOperatorExtra | plusminus)
}

operator<term> { [@name={term},group="operator"]{ term } } 

end { @specialize[@name=end,group="keyword"]<Identifier, "end"> }
kw<term> { @specialize[@name={term},group="keyword"]<Identifier, term> }
boolean { @specialize[@name=BooleanLiteral]<Identifier, "true" | "false"> }

@external tokens layoutExtra from "./index.tokens.js" {
  immediateParen,
  immediateColon,
  immediateBrace,
  immediateBracket,
  immediateDoubleQuote,
  immediateBackquote,
  immediateDot,
  nowhitespace
}

@external tokens terminator from "./index.tokens.js" { terminator }
@external tokens word from "./index.tokens.js" { word }
@external tokens Identifier from "./index.tokens.js" { Identifier }
@external tokens BlockComment from "./index.tokens.js" { BlockComment }
@external tokens tripleStringContent from "./index.tokens.js" { tripleStringContent }
@external tokens stringContent from "./index.tokens.js" { stringContent }
@external tokens commandStringContent from "./index.tokens.js" { commandStringContent }
@external tokens tripleStringContentWithoutInterpolation from "./index.tokens.js" { tripleStringContentWithoutInterpolation }
@external tokens stringContentWithoutInterpolation from "./index.tokens.js" { stringContentWithoutInterpolation }
@external tokens commandStringContentWithoutInterpolation from "./index.tokens.js" { commandStringContentWithoutInterpolation }

@tokens {
  whitespace {
    " " | "\t" | "\n"
  }
  Comment {
    "#" ![\n]*
  }
  Number {
    (
      "0" $[xX] $[0-9a-fA-F] $[0-9a-fA-F_]* |
      $[0-9] $[0-9_]* "."? ($[0-9] $[0-9_]*)? |
      "." $[0-9] $[0-9_]*
    )
    ($[eE] ("-" | "+")? $[0-9]+)?
  }
  powerOperator {
    "."? ("^" | "↑" | "↓" | "⇵" | "⟰" | "⟱" | "⤈" | "⤉" | "⤊" | "⤋" | "⤒" | "⤓" | "⥉" | "⥌" | "⥍" | "⥏" | "⥑" | "⥔" | "⥕" | "⥘" | "⥙" | "⥜" | "⥝" | "⥠" | "⥡" | "⥣" | "⥥" | "⥮" | "⥯" | "￪" | "￬")
  }
  timesOperator {
    "."? ("*" | "/" | "÷" | "%" | "&" | "⋅" | "∘" | "×" | "\\" | "∩" | "∧" | "⊗" | "⊘" | "⊙" | "⊚" | "⊛" | "⊠" | "⊡" | "⊓" | "∗" | "∙" | "∤" | "⅋" | "≀" | "⊼" | "⋄" | "⋆" | "⋇" | "⋉" | "⋊" | "⋋" | "⋌" | "⋏" | "⋒" | "⟑" | "⦸" | "⦼" | "⦾" | "⦿" | "⧶" | "⧷" | "⨇" | "⨰" | "⨱" | "⨲" | "⨳" | "⨴" | "⨵" | "⨶" | "⨷" | "⨸" | "⨻" | "⨼" | "⨽" | "⩀" | "⩃" | "⩄" | "⩋" | "⩍" | "⩎" | "⩑" | "⩓" | "⩕" | "⩘" | "⩚" | "⩜" | "⩞" | "⩟" | "⩠" | "⫛" | "⊍" | "▷" | "⨝" | "⟕" | "⟖" | "⟗")
  }
  plusOperatorExtra {
    "|||" | "⊕" | "⊖" | "⊞" | "⊟" | "|++|" | "∪" | "∨" | "⊔" | "±" | "∓" | "∔" | "∸" | "≂" | "≏" | "⊎" | "⊻" | "⊽" | "⋎" | "⋓" | "⧺" | "⧻" | "⨈" | "⨢" | "⨣" | "⨤" | "⨥" | "⨦" | "⨧" | "⨨" | "⨩" | "⨪" | "⨫" | "⨬" | "⨭" | "⨮" | "⨹" | "⨺" | "⩁" | "⩂" | "⩅" | "⩊" | "⩌" | "⩏" | "⩐" | "⩒" | "⩔" | "⩖" | "⩗" | "⩛" | "⩝" | "⩡" | "⩢" | "⩣"
  }
  arrowOperator {
    "--" | "-->" | "."?  ("←" | "→" | "↔" | "↚" | "↛" | "↞" | "↠" | "↢" | "↣" | "↦" | "↤" | "↮" | "⇎" | "⇍" | "⇏" | "⇐" | "⇒" | "⇔" | "⇴" | "⇶" | "⇷" | "⇸" | "⇹" | "⇺" | "⇻" | "⇼" | "⇽" | "⇾" | "⇿" | "⟵" | "⟶" | "⟷" | "⟹" | "⟺" | "⟻" | "⟼" | "⟽" | "⟾" | "⟿" | "⤀" | "⤁" | "⤂" | "⤃" | "⤄" | "⤅" | "⤆" | "⤇" | "⤌" | "⤍" | "⤎" | "⤏" | "⤐" | "⤑" | "⤔" | "⤕" | "⤖" | "⤗" | "⤘" | "⤝" | "⤞" | "⤟" | "⤠" | "⥄" | "⥅" | "⥆" | "⥇" | "⥈" | "⥊" | "⥋" | "⥎" | "⥐" | "⥒" | "⥓" | "⥖" | "⥗" | "⥚" | "⥛" | "⥞" | "⥟" | "⥢" | "⥤" | "⥦" | "⥧" | "⥨" | "⥩" | "⥪" | "⥫" | "⥬" | "⥭" | "⥰" | "⧴" | "⬱" | "⬰" | "⬲" | "⬳" | "⬴" | "⬵" | "⬶" | "⬷" | "⬸" | "⬹" | "⬺" | "⬻" | "⬼" | "⬽" | "⬾" | "⬿" | "⭀" | "⭁" | "⭂" | "⭃" | "⭄" | "⭇" | "⭈" | "⭉" | "⭊" | "⭋" | "⭌" | "￩" | "￫" | "⇜" | "⇝" | "↜" | "↝" | "↩" | "↪" | "↫" | "↬" | "↼" | "↽" | "⇀" | "⇁" | "⇄" | "⇆" | "⇇" | "⇉" | "⇋" | "⇌" | "⇚" | "⇛" | "⇠" | "⇢")
  }
  bitshiftOperator {
    ">>" | "<<" | ">>>"
  }
  comparisonOperator {
    "|<:|" | "|>:|" | "."?  (">" | "<" | ">=" | "≥" | "<=" | "≤" | "==" | "===" | "≡" | "!=" | "≠" | "!==" | "≢" | "∈" | "∉" | "∋" | "∌" | "⊆" | "⊈" | "⊂" | "⊄" | "⊊" | "∝" | "∊" | "∍" | "∥" | "∦" | "∷" | "∺" | "∻" | "∽" | "∾" | "≁" | "≃" | "≂" | "≄" | "≅" | "≆" | "≇" | "≈" | "≉" | "≊" | "≋" | "≌" | "≍" | "≎" | "≐" | "≑" | "≒" | "≓" | "≖" | "≗" | "≘" | "≙" | "≚" | "≛" | "≜" | "≝" | "≞" | "≟" | "≣" | "≦" | "≧" | "≨" | "≩" | "≪" | "≫" | "≬" | "≭" | "≮" | "≯" | "≰" | "≱" | "≲" | "≳" | "≴" | "≵" | "≶" | "≷" | "≸" | "≹" | "≺" | "≻" | "≼" | "≽" | "≾" | "≿" | "⊀" | "⊁" | "⊃" | "⊅" | "⊇" | "⊉" | "⊋" | "⊏" | "⊐" | "⊑" | "⊒" | "⊜" | "⊩" | "⊬" | "⊮" | "⊰" | "⊱" | "⊲" | "⊳" | "⊴" | "⊵" | "⊶" | "⊷" | "⋍" | "⋐" | "⋑" | "⋕" | "⋖" | "⋗" | "⋘" | "⋙" | "⋚" | "⋛" | "⋜" | "⋝" | "⋞" | "⋟" | "⋠" | "⋡" | "⋢" | "⋣" | "⋤" | "⋥" | "⋦" | "⋧" | "⋨" | "⋩" | "⋪" | "⋫" | "⋬" | "⋭" | "⋲" | "⋳" | "⋴" | "⋵" | "⋶" | "⋷" | "⋸" | "⋹" | "⋺" | "⋻" | "⋼" | "⋽" | "⋾" | "⋿" | "⟈" | "⟉" | "⟒" | "⦷" | "⧀" | "⧁" | "⧡" | "⧣" | "⧤" | "⧥" | "⩦" | "⩧" | "⩪" | "⩫" | "⩬" | "⩭" | "⩮" | "⩯" | "⩰" | "⩱" | "⩲" | "⩳" | "⩵" | "⩶" | "⩷" | "⩸" | "⩹" | "⩺" | "⩻" | "⩼" | "⩽" | "⩾" | "⩿" | "⪀" | "⪁" | "⪂" | "⪃" | "⪄" | "⪅" | "⪆" | "⪇" | "⪈" | "⪉" | "⪊" | "⪋" | "⪌" | "⪍" | "⪎" | "⪏" | "⪐" | "⪑" | "⪒" | "⪓" | "⪔" | "⪕" | "⪖" | "⪗" | "⪘" | "⪙" | "⪚" | "⪛" | "⪜" | "⪝" | "⪞" | "⪟" | "⪠" | "⪡" | "⪢" | "⪣" | "⪤" | "⪥" | "⪦" | "⪧" | "⪨" | "⪩" | "⪪" | "⪫" | "⪬" | "⪭" | "⪮" | "⪯" | "⪰" | "⪱" | "⪲" | "⪳" | "⪴" | "⪵" | "⪶" | "⪷" | "⪸" | "⪹" | "⪺" | "⪻" | "⪼" | "⪽" | "⪾" | "⪿" | "⫀" | "⫁" | "⫂" | "⫃" | "⫄" | "⫅" | "⫆" | "⫇" | "⫈" | "⫉" | "⫊" | "⫋" | "⫌" | "⫍" | "⫎" | "⫏" | "⫐" | "⫑" | "⫒" | "⫓" | "⫔" | "⫕" | "⫖" | "⫗" | "⫘" | "⫙" | "⫷" | "⫸" | "⫹" | "⫺" | "⊢" | "⊣" | "⟂")
  }
  assignOperator {
    ":=" | "~" | "$=" | "."? ("=" | "+=" | "-=" | "*=" | "/=" | "//=" | "|=|" | "^=" | "÷=" | "%=" | "<<=" | ">>=" | ">>>=" | "||=|" | "&=" | "⊻=" | "≔" | "⩴" | "≕")
  }
  unaryOperatorExtra {
    ">:" | "!" | "~" | "¬" | "√" | "∛" | "∜"
  }
  plusminus {
    "+" | "-"
  }
  Character {
    "'" ("\\" !['] | ![']) "'"
  }
  ternary1 {
    whitespace+ "?" whitespace+
  }
  ternary2 {
    whitespace+ ":" whitespace+
  }
  @precedence {
    Number,
    comparisonOperator,
    "=",
    assignOperator,
    unaryOperatorExtra,
    plusOperatorExtra,
    ".",
    ternary1,
    ternary2,
    whitespace
  }
}
